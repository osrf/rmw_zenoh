# Copyright 2020 Open Source Robotics Foundation, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

###################################################################################################
#
# CMake script for finding microcdr
#
# Input variables:
#
# - microcdr_ROOT_DIR (optional): When specified, header files and libraries will be searched for in
#     ${microcdr_ROOT_DIR}/include
#     ${microcdr_ROOT_DIR}/libs
#   respectively, and the default CMake search order will be ignored. When unspecified, the default
#   CMake search order is used.
#   This variable can be specified either as a CMake or environment variable. If both are set,
#   preference is given to the CMake variable.
#   Use this variable for finding packages installed in a nonstandard location, or for enforcing
#   that one of multiple package installations is picked up.
#
#
# Cache variables (not intended to be used in CMakeLists.txt files)
#
# - microcdr_INCLUDE_DIR: Absolute path to package headers.
# - microcdr_LIBRARY: Absolute path to library.
#
#
# Output variables:
#
# - microcdr_FOUND: Boolean that indicates if the package was found
# - microcdr_INCLUDE_DIRS: Paths to the necessary header files
# - microcdr_LIBRARIES: Package libraries
#
#
# Example usage:
#
#  find_package(microcdr)
#  if(NOT microcdr_FOUND)
#    # Error handling
#  endif()
#  ...
#  include_directories(${microcdr_INCLUDE_DIRS} ...)
#  ...
#  target_link_libraries(my_target ${microcdr_LIBRARIES})
#
###################################################################################################

if(microcdr_FOUND)
  return()
endif()

# Get package location hint from environment variable (if any)
if(NOT microcdr_ROOT_DIR AND DEFINED ENV{microcdr_ROOT_DIR})
    set(microcdr_ROOT_DIR "$ENV{microcdr_ROOT_DIR}" CACHE PATH
        "microcdr base directory location (optional, used for nonstandard installation paths)")
endif()

if(microcdr_ROOT_DIR)
  set(microcdr_INCLUDE_PATH PATHS "${microcdr_ROOT_DIR}/include" NO_DEFAULT_PATH)
  set(microcdr_LIBRARY_PATH PATHS "${microcdr_ROOT_DIR}/lib" NO_DEFAULT_PATH)
else()
  set(microcdr_INCLUDE_PATH "")
  set(microcdr_LIBRARY_PATH "")
endif()

# Search for headers and the library
find_path(microcdr_INCLUDE_DIR NAMES "ucdr/microcdr.h" ${microcdr_INCLUDE_PATH})
find_library(microcdr_LIBRARY NAMES microcdr ${microcdr_LIBRARY_PATH})

mark_as_advanced(microcdr_INCLUDE_DIR microcdr_LIBRARY)

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(microcdr DEFAULT_MSG microcdr_INCLUDE_DIR microcdr_LIBRARY)

set(microcdr_FOUND ${MICROCDR_FOUND}) # Enforce case-correctness: Set appropriately cased variable...
unset(MICROCDR_FOUND) # ...and unset uppercase variable generated by find_package_handle_standard_args

if(${microcdr_FOUND})
  set(microcdr_INCLUDE_DIRS ${microcdr_INCLUDE_DIR})
  set(microcdr_LIBRARIES ${microcdr_LIBRARY})

  add_library(microcdr::microcdr UNKNOWN IMPORTED)
  set_property(TARGET microcdr::microcdr PROPERTY IMPORTED_LOCATION ${microcdr_LIBRARY})
  set_property(TARGET microcdr::microcdr PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${microcdr_INCLUDE_DIR})
  list(APPEND microcdr_TARGETS microcdr::microcdr)
endif()
